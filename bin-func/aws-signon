#! /usr/bin/env zsh
#compdef aws-signon

aws-signon() {
    emulate -L zsh
    set -uo pipefail
    setopt err_return extended_glob null_glob typeset_to_unset warn_create_global
    unsetopt short_loops

    local _usage='Usage: aws-signon [-f|--force] [-h|--help] [-c|--command <command>] [--logged-in-command <command>] <profile>

Options:
  -f, --force                   Force login even if already signed in
  -h, --help                    Show this help message
  -c, --command <command>       Alternative command to use to sign on
  --logged-in-command <command> Alternative command to use for checking if alreay logged in
  <profile>                     AWS profile to sign into

Notes:
the command provided to -c/--command/--logged-in-command should just need the profile added as a flag.

If not provided with -c/--command/--logged-in-command will fall back to the aws cli.

Examples:
  aws-signon -f dev
  aws-signon dev
  aws-signon dev -f'

    eval "$(dot-parse-opts --dot-parse-opts-init)"

    short_to_long_flags[f]=force
    flags[force]=0
    max_positional_count=1
    option_args[command]=str
    short_to_long_opts[c]=command
    option_args[logged-in-command]=str
    dot_parse_opts_errors[too-many-positional]='Profile can only be specified once'

    local dot_parse_code=0
    dot-parse-opts "$@" || dot_parse_code=$?
    if (( -1 == dot_parse_code )); then # Help was output
        return 0
    elif (( dot_parse_code )); then
        return $dot_parse_code
    fi

    set -- "${positional_args[@]}"

    if [[ ! -v 'options[command]' ]]; then
        options[command]='aws sso login --profile'
    fi

    local cmd_array=("${(z)options[command]}")
    if ! command -v "${cmd_array[1]}" &> /dev/null; then
        print-header -e "Command '${cmd_array[1]}' not found"
        return 1
    fi

    export AWS_PROFILE
    if (( $# )); then
        AWS_PROFILE="$1"
        set --
    elif [[ -v AWS_PROFILE ]] && [[ -n "${AWS_PROFILE}" ]]; then
        print "No profile specified, using \$AWS_PROFILE: ${AWS_PROFILE}"
    else
        unset AWS_PROFILE
        print-header -e 'No profile specified & $AWS_PROFILE is not available!'
        print "${_usage}"
        return 1
    fi

    print-header green --icon ðŸ” "Signing into AWS: ${AWS_PROFILE}"

    if (( ! flags[force] )); then
        if [[ ! -v 'options[logged-in-command]' ]]; then
            options[logged-in-command]='aws sts get-caller-identity --profile'
        fi

        local logged_in_cmd_array=("${(z)options[logged-in-command]}")
        if ! command -v "${logged_in_cmd_array[1]}" &> /dev/null; then
            print-header -e "Command '${logged_in_cmd_array[1]}' not found"
            return 1
        fi

        local logged_in_cmd="${logged_in_cmd_array[1]}"
        local logged_in_args=("${logged_in_cmd_array[@]:1}" "${AWS_PROFILE}")

        if $logged_in_cmd "${logged_in_args[@]}" > /dev/null; then
            print "already signed in as ${AWS_PROFILE}"
            return 0
        fi

        print "not signed in, attempting SSO login for '${AWS_PROFILE}'..."
    fi

    local cmd="${cmd_array[1]}"
    local args=("${cmd_array[@]:1}" "${AWS_PROFILE}")

    $cmd "${args[@]}"
}

aws-signon "$@"
