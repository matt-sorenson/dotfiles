#! /usr/bin/env zsh
#compdef file-rep-headers

file-rep-headers() {
    emulate -L zsh
    set -uo pipefail
    setopt err_return extended_glob typeset_to_unset warn_create_global 
    unsetopt short_loops

    local _usage='Usage file-rep-headers --header <header> --i <text> <file>

Replace the contents of a file between the start/end markers.

Options:
  -m, --marker <marker>  The Characters to use for the start/end headers. (default: #)
  --header <text>.       The text to use for the header.

  -f, --file <file>      The file to modify.
  -i, --insert <text>    The text to insert between the markers.
  --finsert <file>       The file to insert between the markers.

  -h, --help    Show this message

Notes:
Will look for headers in the form of:
################################################################################
## Start <header>
################################################################################

################################################################################
## End <header>
################################################################################

If your file uses a different header character than # you can specify it with the -m/--marker option.'

    eval "$(dot-parse-opts --dot-parse-opts-init)"

    short_to_long_opts[f]=file

    option_args[insert]=str
    short_to_long_opts[i]=insert
    option_args[finsert]=file

    option_args[header]=str
    short_to_long_opts[h]=header
    option_args[marker]=str
    short_to_long_opts[m]=marker

    min_positional_count=1
    max_positional_count=1

    local dot_parse_code=0
    dot-parse-opts "$@" || dot_parse_code=$?
    if (( -1 == dot_parse_code )); then
        return 0
    elif (( dot_parse_code )); then
        return $dot_parse_code
    fi

    set -- "${positional_args[@]}"
    local filename="$1"

    if [[ -f "$filename" && ! -r "$filename" ]]; then
        print-header -e "File not readable: ${filename}"
        return 1
    elif [[ ! -v options[header] ]]; then
        print-header -e 'Header text is required. Use --header <text> to specify the header.'
        return 1
    elif [[ ! -v options[insert] && ! -v options[finsert] ]]; then
        print-header -e 'Insert text is required. Use --insert <text> or --finsert <file> to specify the text to insert.'
        return 1
    elif [[ -v options[insert] && -v options[finsert] ]]; then
        print-header -e 'Cannot use both --insert and --finsert options at the same time.'
        return 1
    else
        if [[ ! -f "$filename" ]]; then
            local dir="${filename:h}"
            mkdir -p "${dir}"
            touch "$filename"
            unset dir
        fi
    fi

    if [[ ! -v options[marker] ]]; then
        if [[ "$filename" == *.lua ]]; then
            options[marker]='-'
        else
            options[marker]='#'
        fi
    fi

    local header_length=80
    local pad="$(printf "%*s" "$header_length" '' | tr ' ' "${options[marker]}")"
    local header_wrap="$(print -- "${pad}")"
    unset pad

    local start_header="${header_wrap}
${options[marker]}${options[marker]} Start ${options[header]}
${header_wrap}"
    local end_header="${header_wrap}
${options[marker]}${options[marker]} End ${options[header]}
${header_wrap}"

    local start_line=$(awk "/^${options[marker]}{80}/ { getline; if (\$0 ~ /^${options[marker]}${options[marker]} Start ${options[header]}\$/) { getline; print NR } }" "$filename")
    local end_line=$(  awk "/^${options[marker]}{80}/ { getline; if (\$0 ~ /^${options[marker]}${options[marker]} End ${options[header]}\$/) { getline; print NR+1 } }" "$filename")

    if [[ -z $start_line || -z $end_line ]] || (( ! start_line || ! end_line )); then
        if [[ -s "${filename}" ]]; then
            print 'No headers found, adding new headers.'
        else
            print 'Creating file.'
        fi

        if [[ -s "${filename}" ]]; then
            # try stripping all whitespace from file, if theres anything left
            # add newlines to keep it separated.
            local contents="${(L)$(<"$filename")//[[:space:]]/}"
            if [[ -n "${contents}" ]]; then
                print "\n\n" >> "$filename"
            fi
        fi

        print -- "${start_header}" >> "$filename"

        if [[ -v options[insert] ]]; then
            print -- "${options[insert]}" >> "$filename"
        else
            cat "${options[finsert]}" >> "$filename"
            print >> "$filename"
        fi

        print -- "${end_header}" >> "$filename"
    else
        print 'Headers found, replacing existing headers.'

        local bak_file="${filename}.bak"
        mv "${filename}" "${bak_file}"
        touch "$filename"
        head -n $(( start_line )) "$bak_file" >> "$filename"

        if [[ -v options[insert] ]]; then
            printf "%s\n" "${options[insert]}" >> "$filename"
        else
            cat "${options[finsert]}" >> "$filename"
            print >> "$filename"
        fi

        print -- "${end_header}" >> "$filename"

        sed -n "${end_line},\$p" "$bak_file" >> "$filename"
        print >> "$filename"
    fi
}

file-rep-headers "$@"
