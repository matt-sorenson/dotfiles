#! /usr/bin/env zsh
#compdef brew-find-leafs

brew-find-leafs() {
    emulate -L zsh
    set -uo pipefail
    setopt err_return extended_glob null_glob typeset_to_unset warn_create_global
    unsetopt short_loops

    local _usage="Usage: brew-find-leafs [-h|--help]

    Finds brew formulas that aren't depended on by any other packages.
    This is useful for cleaning up unused dependencies.

    Options:
    -h, --help   Show this help message and exit
    -r, --remove Ask to remove formulas that are not depended on by other formulas
    -q, --quiet  Suppress most output, only show the forumulas that would be removed

    There is explicitly not a -f or --force option as this script can't tell if you
    manually installed a formula or if it was installed as a dependency."

    if ! command -v brew &> /dev/null; then
        print-header "Can't find the 'brew' command"
        return 1
    fi

    eval "$(dot-parse-opts --dot-parse-opts-init)"

    short_to_long_flags[q]=quiet
    short_to_long_flags[r]=remove
    flags[quiet]=0
    flags[remove]=0

    local dot_parse_code=0
    dot-parse-opts "$@" || dot_parse_code=$?
    if (( -1 == dot_parse_code )); then # Help was output
        return 0
    elif (( dot_parse_code )); then
        return $dot_parse_code
    fi

    if (( flags[quiet] && flags[remove] )); then
        print "Error: --quiet and --remove are mutually exclusive."
        exit 1
    fi

    if (( ! flags[quiet] )); then
        print-header green "Searching for formulas not depended on by other formulas..."
    fi

    local -a formulas=()
    local -A depended_on=()

    local formula deps dep
    while IFS=: read -r formula deps; do
        formulas+=("$formula")

        deps=("${(s: :)deps}")
        for dep in "${deps[@]}"; do
            depended_on[$dep]=1
        done
    done < <(brew deps --installed)

    for formula in "${formulas[@]}"; do
        if [[ ! -v depended_on[$formula] ]]; then
            if (( flags[remove] )); then
                local input
                if read -q "?Remove ${formula}? [Y/n] "; then
                    print
                    brew remove "${formula}"
                    check_formulas "$(brew deps --1 --installed "${formula}")"
                fi
                unset input
            else
                print "$formula"
            fi
        fi
    done
}

brew-find-leafs "$@"
