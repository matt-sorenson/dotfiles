#! /usr/bin/env zsh
#compdef dot-print-map

dot-print-map() {
    emulate -L zsh
    set -uo pipefail
    setopt err_return extended_glob null_glob typeset_to_unset warn_create_global
    unsetopt short_loops

    local _usage="Usage: dot-print-map [-h|--help] <map_name>

Options:
  -h, --help    Show this message"

    eval "$(dot-parse-opts --dot-parse-opts-init)"

    min_positional_count=1
    max_positional_count=1

    dot_parse_opts_errors[too-many-positional]="You may only specify one map name."
    dot_parse_opts_errors[too-few-positional]="You must specify a map name."

    local dot_parse_code=0
    dot-parse-opts "$@" || dot_parse_code=$?
    if (( -1 == dot_parse_code )); then # Help was output
        return 0
    elif (( dot_parse_code )); then
        return $dot_parse_code
    fi

    local map_name="${positional_args[1]}"

    # Check if variable exists and is an associative array
    if [[ ! -v "$map_name" ]]; then
        print-header -e "Error: '$map_name' is not defined."
        return 1
    fi

    local type
    type="$(typeset -p "$map_name" 2>/dev/null)"
    if [[ "$type" != *-A* ]]; then
        print-header -e "Error: '$map_name' is not an associative array."
        return 1
    fi

    if (( ! ${(P)#map_name} )); then
        print "$map_name is empty"
        return 0
    fi

    printf "%s => %s\n" "${(P@kv)map_name}"
}

dot-print-map "$@"
