#! /usr/bin/env zsh
#compdef ws-ls

ws-ls() {
    emulate -L zsh
    set -uo pipefail
    setopt err_return extended_glob null_glob typeset_to_unset warn_create_global
    unsetopt short_loops

    local _usage="Usage: ws-ls <project> [-- <ls args>]

Arguments:
  <project>   The project directory to list contents of.
  <ls args>   Optional arguments to pass directly to ls.

Options:
  -h, --help    Show this message"

    eval "$(dot-parse-opts --dot-parse-opts-init)"

    flags[describe]=0

    min_positional_count=0
    max_positional_count=1
    allow_extra_args=1

    dot_parse_opts_errors[too-many-positional]="Too many arguments. Can only list 1 project at a time."

    local dot_parse_code=0
    dot-parse-opts "$@" || dot_parse_code=$?
    if (( -1 == dot_parse_code )); then # Help was output
        return 0
    elif (( dot_parse_code )); then
        return $dot_parse_code
    fi

    set -- "${positional_args[@]}"

    if (( flags[describe] )); then
        print -n "List the contents of a project directory"
        return 0
    elif (( ! $# )); then
        print-header -e "No project name provided."
        print "Possible projects:"
        ls "${extra_args[@]}" "${WORKSPACE_ROOT_DIR}"
        return 1
    fi

    local dir="${WORKSPACE_ROOT_DIR}/$1"
    if [[ ! -d "${dir}" ]]; then
        print-header -e "The directory '${dir}' does not exist."
        return 1
    elif [[ "${dir}" != */ ]]; then
        # Ensure the path ends with a slash for ls
        dir="${dir}/"
    fi

    ls "${extra_args[@]}" "${dir}"
}
