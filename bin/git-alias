#!/usr/bin/env zsh
#compdef git-alias

function() {
    emulate -L zsh
    set -uo pipefail
    setopt err_return

    local _usage="Usage: git alias [-h|--help]

List all git aliases configured in the repository.

Options
  -h, --help    Show this help message"

    while (( $# )); do
        case "$1" in
            -h|--help)
                print "${_usage}"
                return 0
                ;;
            *)
                print-header -e "Unknown argument: $1"
                print "${_usage}"
                return 1
                ;;
        esac
        shift
    done

    # Retrieve alias lines: "aliasName command..."
    local git_output=$(git config --get-regexp "^alias\." | cut -f2- -d.)
    # Split into array by newline
    local lines=("${(@f)git_output}")

    # Find longest alias name
    local max=0
    local line
    for line in "${lines[@]}"; do
        local current="${line%% *}"
        (( ${#current} > max )) && max=${#current}
    done

    # Print each with padding: two spaces between alias and command
    for line in "${lines[@]}"; do
        local alias_name="${line%% *}"
        local cmd="${line#* }"
        printf "%-${max}s  %s\n" "$alias_name" "$cmd"
    done
} "$@"
