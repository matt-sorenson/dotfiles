#!/usr/bin/env zsh
#compdef git-alias

function() {
    emulate -L zsh
    set -uo pipefail
    setopt err_return
    setopt null_glob

    local _usage="Usage: git alias [-h|--help]

List all git aliases configured in the repository.

Options
  -h, --help    Show this help message"

    while (( $# )); do
        case "$1" in
            -h|--help)
                print "${_usage}"
                return 0
                ;;
            *)
                print-header -e "Unknown argument: $1"
                print "${_usage}"
                return 1
                ;;
        esac

        # This is unreachable, but included so it's not missed
        # if additional arguments are added.
        # shellcheck disable=SC2317
        shift
    done

    local lines
    # Retrieve alias lines: "alias.aliasName command..."
    lines=("${(@f)$(git config --get-regexp "^alias\.")}")
    # Remove "alias." prefix from each line
    lines=("${lines[@]#alias.}")

    # Find longest alias name
    local max=0
    local line
    for line in "${lines[@]}"; do
        local current="${line%% *}"
        (( ${#current} > max )) && max=${#current}
    done

    # Print each with padding: two spaces between alias and command
    for line in "${lines[@]}"; do
        local alias_name="${line%% *}"
        local cmd="${line#* }"
        printf "%-${max}s  %s\n" "${alias_name}" "${cmd}"
    done
} "$@"
